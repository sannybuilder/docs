# Классы

**Класс** - это группа команд, относящихся к какой-либо сущности в игре: игрокам, актерам, машинам и т.д. Например, класс `Player` объединяет опкоды, которые выполняются над игровым персонажем.

## Общий синтаксис

Синтаксис:  
`<имя класса>.<член класса>(параметры)`

`Имя класса` - название группы опкодов, которая определена в файле `classes.db` для текущего [режима редактирования](../edit-modes.md)  
`Член класса` - одна из команд, входящих в класс  
`Параметры` - 0 и более [параметров](data-types.md), разделенных запятой

```text
Player.SetMinWantedLevel($PLAYER_CHAR, 2)
```

`Player` - имя класса  
`SetMinWantedLevel` - член класса   
`$PLAYER_CHAR, 2` - два параметра для команды `SetMinWantedLevel`

Существует 3 вида членов класса:

* условия
* методы
* свойства

### Условия 

Условные команды в списке, вызываемом нажатием `Ctrl+Space`, обозначаются словом `Check`. Они используются в [условных выражениях](conditions.md):

```text
if
    Player.Defined($PLAYER_CHAR)
jf @anywhere
```

### Методы

Команды, которые используются для выполнения одиночного действия над экземплярами класса, называются методами. Например, перемещение объекта, взрыв машины и т.п.:

```text
Object.PutAt($crate, 10.0, -25.5, 12.2)
Car.Destroy($car)
```

В списке членов класса они обозначаются словом `proc`. 

Особый вид методов - это конструктор. Конструктор создает новый экземпляр класса и сохраняет указатель на него в переменную.

Sanny Builder допускает два эквивалентных друг другу варианта написать конструктор:

```text
Player.Create($PLAYER_CHAR, #NULL, 2488.5601, -1666.84, 13.38)
```

```text
$PLAYER_CHAR = Player.Create(#NULL, 2488.5601, -1666.84, 13.38)
```

### Свойства

Свойства служат для чтения или изменения отдельных атрибутов экземпляра класса.

Например, свойство `.Money` класса `Player` позволяет работать с количеством денег у игрока:

```text
Player($PLAYER_CHAR).Money += 1000000 // добавить деньги
Player($PLAYER_CHAR).Money > 461@ // проверить счет игрока
4@ = Player($PLAYER_CHAR).Money // записать текущую сумму денег в переменную
```

{% hint style="warning" %}
В текущей версии существует ограничение на использование пробелов в [строковых литералах](data-types.md#strokovye-literaly) в параметрах свойств. Компилятор игнорирует такие пробелы:

```text
0@ = File.Open("file name","wb")
```

будет скомпилировано как:

```text
0@ = File.Open("filename","wb")
```
{% endhint %}

## Экземпляры класса

Практически все члены классов в качестве первого параметра принимают переменную, которая хранит указатель на экземпляр класса - ту сущность в игре, над которой выполняется эта команда:

```text
Player.Build($PLAYER_CHAR)
```

`$PLAYER_CHAR` - экземпляр класса. 

При этом некоторые внутриигровые элементы существуют в единственном экземпляре. Например, камера, которая определяет поле зрения игрока. Члены классов для таких сущностей не требуют экземпляра класса:

```text
Camera.SetBehindPlayer()
```

### Объявление экземпляров класса

Переменные можно [объявлять](variables.md#konstrukciya-var-end) с типом - именем класса:

```text
var
    $PLAYER_CHAR: Player
end
```

Это указывает компилятору на то, что переменная `$PLAYER_CHAR` является экземпляром класса `Player`. Такую переменную можно использовать вместо имени класса:

```text
if
    $PLAYER_CHAR.Defined
jf @anywhere
```

{% hint style="warning" %}
Если переменная используется вместо имени класса, она также компилируется как первый параметр, поэтому дублировать её в списке параметров не нужно:

```text
$PLAYER_CHAR.SetClothes("PLAYER_FACE", "HEAD", Head)
```

эквивалентно:

```text
Player.SetClothes($PLAYER_CHAR, "PLAYER_FACE", "HEAD", Head)
```
{% endhint %}

Переменные - экземпляры класса могут быть переобъявлены с другим типом.

### Класс `Model` 

[Имена моделей](data-types.md#imena-modelei) являются экземплярами класса `Model`:

```text
#AK47.Load
  
:loop
wait 0
if
  #AK47.Available
jf @loop
```

эквивалентно:

```text
Model.Load(#AK47)

:loop
wait 0
if
    Model.Available(#AK47)
jf @loop
```

## Константы класса

Некоторые параметры в членах классов могут задаваться в виде заранее определенных констант. Это делает код более читаемым:

```text
Player.SetClothes($PLAYER_CHAR, "VEST", "VEST", Torso)
```

Последний параметр \(`Torso`\) - это константа для метода `SetClothes`, которая будет заменена на `0` при компиляции. Константы классов определены в файле `classes.db`.

{% hint style="info" %}
Параметр, для которого существует список констант, обозначается словом `Extended` в списке членов класса и подсказке при наборе параметров. 

Список доступных констант можно вызвать через `Ctrl+Space`, если курсор стоит в месте, где используется такой параметр.
{% endhint %}

